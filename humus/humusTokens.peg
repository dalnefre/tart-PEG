#
# Lexical tokens for the Humus programming language (cf. humusSyntax.peg)
#

tokens  <- token* _ EOF
token   <- _ (symbol / number / char / string / ident / punct)

symbol  <- '#' (punct / name)

number  <- '-'? [0-9]+ ('#' [0-9a-zA-Z]+)?

char    <- "'" (!"'" qchar) "'"
string  <- '"' (!'"' qchar)+ '"'
qchar   <- '\\' [nrt'"\[\]\\]
         / '\\' 'u' [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F]
         / !'\\' .

ident   <- name
name    <- [-0-9a-zA-Z!%&'*+/?@^_~]+
punct   <- [#$(),.:;=\[\\\]]

_       <- (comment / space)*               # optional whitespace
comment <- '#' space (!EOL .)*
space   <- [ \t-\r]
EOL     <- '\n'
         / '\r' '\n'?
EOF     <- !.
